#!/usr/bin/env python
# SConscript for Clustering Tools
#  This file is part of LOOS.
#
#  LOOS (Lightweight Object-Oriented Structure library)
#  Copyright (c) 2010 Tod D. Romo
#  Department of Biochemistry and Biophysics
#  School of Medicine & Dentistry, University of Rochester
#
#  This package (LOOS) is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation under version 3 of the License.
#
#  This package is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


import sys
import os

Import('env')
Import('loos')

clone = env.Clone()
clone.Prepend(LIBS = [loos])

# Scripts

scripts = ''

# Stand-alone apps...

list = []
apps = 'cluster-kgs'

# for name in Split(apps):
#     fname = name + '.cpp'
#     prog = clone.Program(fname)
#     list.append(prog)


### Library generation
# Be sure to add new modules/headers here!!!
library_sources = 'ClusteringUtils.cpp HAC.cpp AverageLinkage.cpp KGS.cpp ClusteringOptions.cpp'
library_headers = 'Clustering.hpp ClusteringUtils.hpp HAC.hpp AverageLinkage.hpp KGS.hpp ClusteringOptions.hpp'

loos_clustering = clone.Library('loos_clustering', Split(library_sources))
clone.Prepend(LIBS=['loos_clustering'])
clone.Prepend(LIBPATH=['#/Packages/Clustering'])
clone.Prepend(CPPPATH='#/Packages/Clustering')


# Tools requiring the above library
dependent = 'cluster-kgs'
for name in Split(dependent):
    fname = name + '.cpp'
    prog = clone.Program(fname)
    list.append(prog)


### Installation specific

PREFIX = env['PREFIX']
bin_path = os.path.join(PREFIX, "bin")

clustering_tools = env.Install(bin_path, list)
# if darwin, run install_name_tool so the binaries can find the library
if env['host_type'] == 'Darwin':
    for app in Split(apps):
        app_path = os.path.join(bin_path, app)
        env.AddPostAction(app_path, "install_name_tool -change libloos.dylib @rpath/libloos.dylib " + app_path)


# TODO: this doesn't do anything, since there are no scripts right now
for file in Split(scripts):
    PREFIX_BIN = PREFIX + '/bin/'
    Command(PREFIX_BIN + file, file,
            [
            Copy("$TARGET", "$SOURCE"),
            Chmod("$TARGET", 0o755)
            ])

env.Alias('clustering_package', clustering_tools)


Return('list')
