#!/usr/bin/env python
# SConscript for Convergence Tools
#
#  LOOS (Lightweight Object-Oriented Structure library)
#  Copyright (c) 2010 Tod D. Romo
#  Department of Biochemistry and Biophysics
#  School of Medicine & Dentistry, University of Rochester
#
#  This package (LOOS) is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation under version 3 of the License.
#
#  This package is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


import sys
import os

Import('env')
Import('loos')

clone = env.Clone()
clone.Prepend(LIBS = [loos])

# Scripts

scripts = 'bootstrap_overlap.pl effsize.pl'

# Stand-alone apps...

list = []
apps = 'fidpick sortfids hierarchy neff block_average avgconv block_avgconv expfit'
apps += ' chist'

for name in Split(apps):
    fname = name + '.cpp'
    prog = clone.Program(fname)
    list.append(prog)


### Library generation
# Be sure to add new modules/headers here!!!
library_sources = 'fid-lib.cpp'
library_headers = 'bcomlib.hpp fid-lib.hpp'

loos_convergence = clone.Library('loos_convergence', Split(library_sources))
clone.Prepend(LIBS=['loos_convergence'])
clone.Prepend(LIBPATH=['#/Packages/Convergence'])


# Tools requiring the above library
dependent = 'bcom boot_bcom ufidpick assign_frames decorr_time coscon qcoscon rsv-coscon'
for name in Split(dependent):
    fname = name + '.cpp'
    prog = clone.Program(fname)
    list.append(prog)


### Installation specific

PREFIX = env['PREFIX']


bin_path = os.path.join(PREFIX, "bin")
conv_tools = env.Install(bin_path, list)
# if darwin, run install_name_tool so the binaries can find the library
if env['host_type'] == 'Darwin':
    for app in Split(apps):
        app_path = os.path.join(bin_path, app)
        env.AddPostAction(app_path, "install_name_tool -change libloos.dylib @rpath/libloos.dylib " + app_path)

for file in Split(scripts):
    PREFIX_BIN = PREFIX + '/bin/'
    Command(PREFIX_BIN + file, file,
            [
            Copy("$TARGET", "$SOURCE"),
            Chmod("$TARGET", 0o755)
            ])

env.Alias('convergence_package', conv_tools)


Return('list')
