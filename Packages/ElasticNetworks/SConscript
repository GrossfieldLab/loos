#!/usr/bin/env python
#  This file is part of LOOS.
#
#  LOOS (Lightweight Object-Oriented Structure library)
#  Copyright (c) 2009 Tod D. Romo
#  Department of Biochemistry and Biophysics
#  School of Medicine & Dentistry, University of Rochester
#
#  This package (LOOS) is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation under version 3 of the License.
#
#  This package is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.



import sys
import os

Import('env')
Import('loos')

clone = env.Clone()
clone.Prepend(LIBS = [loos])

apps = 'gnm enmovie psf-masses heavy-ca eigenflucc'

list = []

for name in Split(apps):
    fname = name + '.cpp'
    prog = clone.Program(fname)
    list.append(prog)


### Library generation
# Be sure to add new modules/headers here!!!
library_sources = 'spring_functions.cpp enm-lib.cpp vsa-lib.cpp'
library_headers = 'anm-lib.hpp enm-lib.hpp spring_functions.hpp vsa-lib.hpp'

loos_enm = clone.Library('loos_enm', Split(library_sources))
clone.Prepend(LIBS=['loos_enm'])
clone.Prepend(LIBPATH=['#/Packages/ElasticNetworks'])


# Tools requiring the library
vsa = clone.Program('vsa.cpp')
list.append(vsa)

anm = clone.Program('anm.cpp')
list.append(anm)


# Update to include the above apps
apps = apps + ' vsa anm'


### Installation specific

PREFIX = env['PREFIX']
bin_path = os.path.join(PREFIX, "bin")

enm_tools = env.Install(bin_path, Split(apps))
# if darwin, run install_name_tool so the binaries can find the library
if env['host_type'] == 'Darwin':
    for app in Split(apps):
        app_path = os.path.join(bin_path, app)
        env.AddPostAction(app_path, "install_name_tool -change libloos.dylib @rpath/libloos.dylib " + app_path)



env.Alias('elastic_networks_package', enm_tools)

Return('list')
