#!/usr/bin/env python
#  This file is part of LOOS.
#
#  LOOS (Lightweight Object-Oriented Structure library)
#  Copyright (c) 2010 Tod D. Romo
#  Department of Biochemistry and Biophysics
#  School of Medicine & Dentistry, University of Rochester
#
#  This package (LOOS) is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation under version 3 of the License.
#
#  This package is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.



import sys
import os

Import('env')
Import('loos')

clone = env.Clone()
clone.Prepend(LIBS = [loos])

PREFIX = env['PREFIX']

list = []

hcore = clone.Object('hcore.cpp')

hcorr = clone.Program('hcorrelation', ['hcorrelation.cpp', hcore])
list.append(hcorr)

hbonds = clone.Program('hbonds', ['hbonds.cpp', hcore])
list.append(hbonds)

hmatrix = clone.Program('hmatrix', ['hmatrix.cpp', hcore])
list.append(hmatrix)

hcontacts = clone.Program('hcontacts', ['hcontacts.cpp', hcore])
list.append(hcontacts)


install_apps = 'hbonds hcorrelation hmatrix hcontacts'

scripts = 'hoccupancies.pl'
for file in Split(scripts):
    PREFIX_BIN = PREFIX + '/bin/'
    Command(PREFIX_BIN + file, file,
            [
            Copy("$TARGET", "$SOURCE"),
            Chmod("$TARGET", 0o755)
            ])

bin_path = os.path.join(PREFIX, "bin")
loos_tools = env.Install(bin_path, Split(install_apps))
# if darwin, run install_name_tool so the binaries can find the library
if env['host_type'] == 'Darwin':
    for app in Split(install_apps):
        app_path = os.path.join(bin_path, app)
        env.AddPostAction(app_path, "install_name_tool -change libloos.dylib @rpath/libloos.dylib " + app_path)

env.Alias('h_tools_install', loos_tools)

Return('list')
